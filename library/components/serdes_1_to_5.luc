/******************************************************************************

   The MIT License (MIT)

   Copyright (c) 2015 Embedded Micro

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.

   *****************************************************************************/

module serdes_1_to_5 (
    input data_p,
    input data_n,
    input ioclk,
    input strobe,
    input rst,
    input gclk,
    input bitslip,
    output data[5]
  ) {
  
  .clk(gclk), .rst(rst) {
    fsm state = {INIT, CAL, WAIT_CAL, WAIT_RESET, WAIT_ENABLE, CAL_SLAVE, WAIT_CAL_SLAVE_START, WAIT_ALL};
    dff io_init;
    dff ctr[9];
    dff pdctr[5] (#INIT(5b10000));
    dff flag;
  }
  
  xil_IBUFDS bufds (
    .I(data_p),
    .IB(data_n)
  );
  
  xil_IODELAY2 io_master (
    .CLK(gclk),
    .IOCLK0(ioclk),
    .IOCLK1(0),
    #DATA_RATE("SDR"),
    #IDELAY_VALUE(0),
    #IDELAY2_VALUE(0),
    #IDELAY_MODE("NORMAL"),
    #ODELAY_VALUE(0),
    #IDELAY_TYPE("DIFF_PHASE_DETECTOR"),
    #COUNTER_WRAPAROUND("STAY_AT_LIMIT"),
    #DELAY_SRC("IDATAIN"),
    #SERDES_MODE("MASTER")
  );
  
  xil_IODELAY2 io_slave (
    .CLK(gclk),
    .IOCLK0(ioclk),
    .IOCLK1(0),
    #DATA_RATE("SDR"),
    #IDELAY_VALUE(0),
    #IDELAY2_VALUE(0),
    #IDELAY_MODE("NORMAL"),
    #ODELAY_VALUE(0),
    #IDELAY_TYPE("DIFF_PHASE_DETECTOR"),
    #COUNTER_WRAPAROUND("WRAPAROUND"),
    #DELAY_SRC("IDATAIN"),
    #SERDES_MODE("SLAVE")
  );
  
  xil_ISERDES2 serdes_master (
    .CLK0(ioclk),
    .CLK1(0),
    .RST(rst),
    .CLKDIV(gclk),
    .D(io_master.DATAOUT),
    #DATA_WIDTH(5),
    #DATA_RATE("SDR"),
    #BITSLIP_ENABLE("TRUE"),
    #SERDES_MODE("MASTER"),
    #INTERFACE_TYPE("RETIMED")
  );
  
  xil_ISERDES2 serdes_slave (
    .CLK0(ioclk),
    .CLK1(0),
    .RST(rst),
    .CLKDIV(gclk),
    .D(io_slave.DATAOUT),
    #DATA_WIDTH(5),
    #DATA_RATE("SDR"),
    #BITSLIP_ENABLE("TRUE"),
    #SERDES_MODE("SLAVE"),
    #INTERFACE_TYPE("RETIMED")
  );
  
  
  always {
    io_master.RST = 0;
    io_slave.RST = 0;
    io_master.T = 1;
    io_slave.T = 1;
    io_master.INC = 0;
    io_slave.INC = 0;
    io_master.CE = 0;
    io_slave.CE = 0;
    io_master.CAL = 0;
    io_slave.CAL = 0;
    io_master.ODATAIN = 0;
    io_slave.ODATAIN = 0;
    io_master.IDATAIN = bufds.O;
    io_slave.IDATAIN = bufds.O;
    
    serdes_master.CE0 = 1;
    serdes_slave.CE0 = 1;
    serdes_master.IOCE = strobe;
    serdes_slave.IOCE = strobe;
    serdes_master.SHIFTIN = serdes_slave.SHIFTOUT;
    serdes_slave.SHIFTIN = serdes_master.SHIFTOUT;
    serdes_master.BITSLIP = bitslip;
    serdes_slave.BITSLIP = bitslip;
    
    data = c{serdes_master.Q4, serdes_master.Q3, serdes_master.Q2, serdes_master.Q1, serdes_slave.Q4};
    
    ctr.d = ctr.q + 1;
    
    if (ctr.q[5])
      io_init.d = 1;
    
    if (ctr.q[8])
      ctr.d = 0;
    
    case (state.q){
      state.INIT:
        if (io_init.q)
          state.d = state.CAL;
      state.CAL:
        io_master.CAL = 1;
        io_slave.CAL = 1;
        if (io_slave.BUSY == 1)
          state.d = state.WAIT_CAL;
      state.WAIT_CAL:
        if (io_slave.BUSY == 0){
          io_master.RST = 1;
          io_slave.RST = 1;
          state.d = state.WAIT_RESET;
        }
      state.WAIT_RESET:
        if (io_slave.BUSY == 0)
          state.d = state.WAIT_ENABLE;
      state.WAIT_ENABLE:
        if (ctr.q[8])
          state.d = state.CAL_SLAVE;
      state.CAL_SLAVE:
        if (io_slave.BUSY == 0) {
          io_slave.CAL = 1;
          state.d = state.WAIT_CAL_SLAVE_START;
        }
      state.WAIT_CAL_SLAVE_START:
        if (io_slave.BUSY)
          state.d = state.WAIT_ALL;
      state.WAIT_ALL:
        if (io_slave.BUSY == 0)
          state.d = state.WAIT_ENABLE;
    }
    
    if (serdes_master.VALID) {
      if (serdes_master.INCDEC && !&pdctr.q)
        pdctr.d = pdctr.q + 1;
      else if (!serdes_master.INCDEC && |pdctr.q)
        pdctr.d = pdctr.q - 1;
    }
    
    if (state.q == state.WAIT_ALL) {
      flag.d = 0;
    } else if (state.q != state.WAIT_ENABLE || io_slave.BUSY) {
      pdctr.d = 5b10000;
    } else if (&pdctr.q && !flag.q) {
      pdctr.d = 5b10000;
      io_master.CE = 1;
      io_slave.CE = 1;
      io_master.INC = 1;
      io_slave.INC = 1;
      flag.d = 1;
    } else if (!|pdctr.q && !flag.q) {
      pdctr.d = 5b10000;
      io_master.CE = 1;
      io_slave.CE = 1;
      io_master.INC = 0;
      io_slave.INC = 0;
      flag.d = 1;
    }
  }
}