/******************************************************************************

   The MIT License (MIT)

   Copyright (c) 2015 Embedded Micro

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.

   *****************************************************************************/

module counter #(
    SIZE = 8 : SIZE > 0, // Width of the output
    DIV = 0  : DIV >= 0, // number of bits to use as divisor
    TOP = 0  : TOP >= 0, // max value, 0 = none
    
    // direction to count, use 1 for up and 0 for down
    UP = 1 : UP == 1 || UP == 0
  )(
    input clk,
    input rst,
    output value[SIZE]
  ) {
  
  .clk(clk), .rst(rst) {
    dff ctr[SIZE+DIV];
  }
  
  const MAX_VALUE = c{TOP, DIVx{1}}; // value when maxed out
  
  always {
    value = ctr.q[SIZE+DIV-1-:SIZE]; // set the output
    
    if (UP) { // when this is an up counter
      ctr.d = ctr.q + 1; // increase
      if (TOP != 0 && ctr.q == MAX_VALUE) { // reached the top?
        ctr.d = 0; // reset
      }
    } else { // down counter
      ctr.d = ctr.q - 1; // decrease
      if (TOP != 0 && ctr.q == 0) { // reached the bottom?
        ctr.d = MAX_VALUE; // reset to top
      }
    }
  }
}